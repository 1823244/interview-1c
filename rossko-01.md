# 1 Блокировки

Документация  
https://its.1c.ru/db/v8std/content/490/hdoc  


//Как отработает код, при одномоментной работе двух потоков по одному контрагенту?  
//Надо записать изменения по всем контрагентам;  
```
Для Каждого Строка Из ТабЗнач Цикл
	КонтСс = справочники.контрагенты.найтипокоду(Строка.Код);
	КонтОб = КонтСс.получитьОБъект();
	контОб.наименование = Строка.Наименование;
	контОб.Записать();
КонецЦикла;
```
Ответ на первый вопрос:  
По условию задачи, код   
```
    КонтОб = КонтСс.получитьОБъект() 
```
выполнился в двух потоках одновременно, до записи объекта в любом из них.  
Один поток запишет успешно, другой получить ошибку оптимистической объектной блокировки:  
```
Операция не может быть выполнена из-за несоответствия версии или отсутствия  
записи базы данных (возможно, запись была изменена или удалена)!
```

MEMENTO  
блокировка данных для редактирования не запрещает запись заблокированных данных в других пользовательских сеансах (или в других экземплярах объекта в этом же сеансе), а лишь не позволяет нескольким объектам одновременно установить блокировку одних и тех же данных. В отличие от транзакционных блокировок данных, пессимистическая блокировка данных для редактирования предназначена для обеспечения конкурентной работы пользователей с объектами информационной базы  
(https://its.1c.ru/db/v8std#content:490:hdoc)  

# Исправление - вариант 1 

Устанавливает ПЕССИМИСТИЧЕСКУЮ блокировку!  
(https://its.1c.ru/db/v837doc#bookmark:dev:TI000000524)  
ВНИМАНИЕ! Если метод Блокировка.Заблокировать() выполняется вне транзакции или в автоматическом режиме управления блокировками в рамках транзакции, то будет сгенерировано исключение.  

```
	Блокировка = Новый БлокировкаДанных;
					
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "Справочник.Контрагенты";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Для Каждого Строка Из ТабЗнач Цикл
		КонтСс = справочники.контрагенты.найтипокоду("000000001");  
		ЭлементБлокировки.УстановитьЗначение("Ссылка",КонтСс);
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			КонтОб = КонтСс.получитьОБъект();  
			контОб.наименование = "5555555555";  
			контОб.Записать();  
			ЗафиксироватьТранзакцию();
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ВызватьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;

```
Второй сеанс будет ждать, пока первый снимет блокировку.  
Если транзакция держится слишком долго (больше таймаута), то второй сеанс получит ошибку:  

```
Конфликт блокировок при выполнении транзакции:  
Превышено максимальное время ожидания предоставления блокировки
```

-----------------------------
тестовый код для консоли  
транзакция нужна для объекта Блокировка, а пауза - для того, чтобы в третьем сеансе увидеть последовательность изменений наименования. Или создать условия для таймаута.  

```
//клиен 1

ТабЗнач = Новый ТаблицаЗначений;
табЗнач.Колонки.Добавить("Код");
ТабЗнач.Добавить().Код = "000000001";


	Блокировка = Новый БлокировкаДанных;
					
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "Справочник.Контрагенты";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Для Каждого Строка Из ТабЗнач Цикл
		КонтСс = справочники.контрагенты.найтипокоду("000000001");  
		ЭлементБлокировки.УстановитьЗначение("Ссылка",КонтСс);

		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			КонтОб = КонтСс.получитьОБъект();  
			контОб.наименование = "клиент 1";//Строка(ТекущаяУниверсальнаяДатаВМиллисекундах());  
			контОб.Записать();  
			СУУ_УниверсальныеСервер.Пауза(30);
			ЗафиксироватьТранзакцию();
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ВызватьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;


```

# Исправление - вариант 2

метод объекта - Заблокировать()  

Второй сеанс не ждет снятия блокировки первым, а выбрасывает ошибку:  

```
Не удалось заблокировать запись. Действие (изменение, удаление или блокировка записи)
не выполнено.
Ошибка блокировки объекта. Объект уже заблокирован:
компьютер: server1, пользователь: Admin, сеанс: 1, 
начат: 23.08.2022 в 9:41:40, приложение: Тонкий клиент
```

Изменения в коде минимальны:  
```
Для Каждого Строка Из ТабЗнач Цикл
		КонтСс = справочники.контрагенты.найтипокоду("000000001");  

		НачатьТранзакцию();
		Попытка
			// убрать
			//Блокировка.Заблокировать();
			КонтОб = КонтСс.получитьОБъект();  
			// добавить
			КонтОб.Заблокировать();

			контОб.наименование = "клиент 1 - "+строка(текущаяДата());
			контОб.Записать();  
			СУУ_УниверсальныеСервер.Пауза(5);
			ЗафиксироватьТранзакцию();
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ВызватьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;

```

Чтобы второй сеанс мог обработать данные, нужно добавить цикл повторения:  
(но следует помнить, что в такой цикл создает нагрузку на процессор)  
```
	Для Каждого Строка Из ТабЗнач Цикл
		КонтСс = справочники.контрагенты.найтипокоду("000000001");  
		
		ОбъектОбработан = Ложь;
		Пока НЕ ОбъектОбработан Цикл
			НачатьТранзакцию();
			Попытка
				КонтОб = КонтСс.получитьОБъект();  
				КонтОб.Заблокировать();
				контОб.наименование = "клиент 2 - "+строка(текущаяДата()); 
				контОб.Записать();  
				СУУ_УниверсальныеСервер.Пауза(10);
				ЗафиксироватьТранзакцию();
				ОбъектОбработан = Истина;
			Исключение
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				//ВызватьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЦикла;
	КонецЦикла;
```

# Исправление - вариант 3

Метод глобального контекста - ЗаблокироватьДанныеДляРедактирования()

Устанавливает ПЕССИМИСТИЧЕСКУЮ блокировку!  
(https://its.1c.ru/db/v837doc#bookmark:dev:TI000000524)  

	Глобальный контекст (Global context)
	ЗаблокироватьДанныеДляРедактирования (LockDataForEdit)
	Синтаксис:
	ЗаблокироватьДанныеДляРедактирования(<Ключ>, <ВерсияДанных>, <ИдентификаторФормы>)
	Параметры:
	<Ключ> (обязательный)
		Тип: 
				- Любая ссылка на объект информационной базы, 
				- РегистрСведенийКлючЗаписи
					.<Имя регистра сведений>,
				- ВнешнийИсточникДанныхТаблицаСсылка
					.<Имя внешнего источника>
						.<Имя таблицы внешнего источника данных>,
				- ВнешнийИсточникДанныхТаблицаКлючЗаписи
					.<Имя внешнего источника>
						.<Имя таблицы внешнего источника данных>. 

		Ссылка на объект, который должен быть заблокирован, или ключ записи независимого регистра сведений, которая должна быть заблокирована.
	<ВерсияДанных> (необязательный)
		Тип: Строка. 
		Версия блокируемого объекта в формате Base64. Используется, если параметр <Ключ> имеет тип Любая ссылка на объект информационной базы. 
		Если указан, то при блокировке объекта проверяется версия объекта базы данных. При несовпадении версии или отсутствии объекта в базе данных генерируется исключение.
	<ИдентификаторФормы> (необязательный)
		Тип: УникальныйИдентификатор. 
		Уникальный идентификатор формы. Если параметр указан, то блокировка устанавливается на все время жизни формы, в которой установлена блокировка. Важно, что если за время отображения формы не были установлены другие блокировки от имени этой формы (при интерактивном редактировании или методом ЗаблокироватьДанныеФормыДляРедактирования), то при закрытии формы блокировка может быть отменена не сразу, а через некоторое время. Блокировка может быть снята:
			- автоматически при закрытии формы или завершении сеанса;
			- с помощью метода глобального контекста РазблокироватьДанныеДляРедактирования с указанием того же идентификатора формы, который указывался для установки блокировки. В этом случае блокировка снимается сразу.
		Если параметр не указан, то блокировка устанавливается на ограниченное время и может быть снята:
			- с помощью метода РазблокироватьДанныеДляРедактирования без указания идентификатора формы;
			- при окончании транзакции, если блокировка установлена в транзакции;
			- при окончании работы встроенного языка (для режимов запуска "Тонкий клиент" и "Веб-клиент"; при возврате управления с сервера);
			- при завершении сеанса.
	Описание:
		Заблокировать данные для редактирования в форме клиентского приложения.
		Вызывает исключение, если объект уже заблокирован, в том числе и методом Заблокировать.
	Доступность:
		Сервер, толстый клиент, внешнее соединение, мобильное приложение (сервер), мобильный автономный сервер.
	См. также:
		Глобальный контекст, метод РазблокироватьДанныеДляРедактирования
	Использование в версии:
		Доступен, начиная с версии 8.2.
		
