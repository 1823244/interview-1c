# 1 Блокировки

//Как отработает код, при одномоментной работе двух потоков по одному контрагенту?  
//Надо записать изменения по всем контрагентам;  
```
Для Каждого Строка Из ТабЗнач Цикл
	КонтСс = справочники.контрагенты.найтипокоду(Строка.Код);
	КонтОб = КонтСс.получитьОБъект();
	контОб.наименование = Строка.Наименование;
	контОб.Записать();
КонецЦикла;
```

Упростим код для консоли:  
```
	КонтСс = справочники.контрагенты.найтипокоду("000000001");  
	КонтОб = КонтСс.получитьОБъект();  
	контОб.наименование = "5555555555";  
	контОб.Записать();  
```

По условию задачи, код   
```    КонтОб = КонтСс.получитьОБъект() ```
выполнился в двух потоках одновременно, до записи объекта в любом из них.  
Один поток запишет успешно, другой получить ошибку оптимистической объектной блокировки:  
```
Операция не может быть выполнена из-за несоответствия версии или отсутствия записи базы данных (возможно, запись была изменена или удалена)!
```

Исправление  

```
	
	
	Блокировка = Новый БлокировкаДанных;
					
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "Справочник.Контрагенты";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Для Каждого Строка Из ТабЗнач Цикл
		КонтСс = справочники.контрагенты.найтипокоду("000000001");  
		ЭлементБлокировки.УстановитьЗначение("Ссылка",КонтСс);
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			КонтОб = КонтСс.получитьОБъект();  
			контОб.наименование = "5555555555";  
			контОб.Записать();  
			ЗафиксироватьТранзакцию();
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ВызватьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;

```

Если транзакция держится слишком долго (больше таймаута), то второй сеанс получит ошибку:  
```
Конфликт блокировок при выполнении транзакции:
Превышено максимальное время ожидания предоставления блокировки
```

-----------------------------
тестовый код для консоли  
транзакция нужна для объекта Блокировка, а пауза - для того, чтобы в третьем сеансе увидеть последовательность изменений наименования. Или создать условия для таймаута.  

```
//клиен 1

ТабЗнач = Новый ТаблицаЗначений;
табЗнач.Колонки.Добавить("Код");
ТабЗнач.Добавить().Код = "000000001";


	Блокировка = Новый БлокировкаДанных;
					
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "Справочник.Контрагенты";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Для Каждого Строка Из ТабЗнач Цикл
		КонтСс = справочники.контрагенты.найтипокоду("000000001");  
		ЭлементБлокировки.УстановитьЗначение("Ссылка",КонтСс);

		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			КонтОб = КонтСс.получитьОБъект();  
			контОб.наименование = "клиент 1";//Строка(ТекущаяУниверсальнаяДатаВМиллисекундах());  
			контОб.Записать();  
			СУУ_УниверсальныеСервер.Пауза(30);
			ЗафиксироватьТранзакцию();
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ВызватьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;


```