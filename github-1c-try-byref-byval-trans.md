В этом файле - примеры работы:
- с попыткой
- с транзакцией
- передачи параметров по ссылке и значению

//По умолчанию, во встроенном языке 1С:Предприятия 8 передача параметров в процедуры и функции осуществляется по ссылке. 

// про передачу по ссылке и по значению:
//https://its.1c.ru/db/metod8dev/content/2606/hdoc

// Особенности при передаче между клиентом и сервером
// https://its.1c.ru/db/v8315doc#bookmark:dev:ti000000151

// Evil Bear
// https://infostart.ru/1c/articles/388527/

&НаКлиенте
Процедура ЗапуститьКод(Команда)
	ЗапуститьКодНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗапуститьКодНаСервере()
	
	//# Работа в попытке
	
	//## Примитивы
	
	//-------------------------------------------------------------
	// Исключение никуда не пробрасываем, а обрабатываем на месте.
	// Изменения переменных сохраняются, даже в случае ошибки!
	
	а = 1;
	Попытка
		а = 2;
		в = 1/0;
	Исключение
		//Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// здесь будет выведено: 2
	сообщить("Попытка. Исключение никуда не пробрасываем, а обрабатываем на месте. Перем а = " + а);
	
	////-------------------------------------------------------------
	//// Исключение пробрасываем наверх.
	//// Изменения переменных сохраняются, даже в случае ошибки!
	//
	//а = 1;
	//Попытка
	//	а = 2;
	//	в = 1/0;
	//Исключение
	//	сообщить("Исключение пробрасываем наверх. Перем а = " + а);
	//	ВызватьИсключение;
	//КонецПопытки; 
	
	
	//-------------------------------------------------------------
	// Ошибка в вызываемом методе.
	// Исключение пробрасываем наверх.
	// Изменения переменных сохраняются, даже в случае ошибки!
	
	а = 1;
	Попытка
		ФункцияСОшибкойВПопытке(а);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// Перем "а" будет содержать значение 2 (изменено в вызываемом методе)
	сообщить("Попытка. Исключение было в вызываемом методе. Перем а: "+а);
	
	//-------------------------------------------------------------
	// Передача по значению
	// Ошибка в вызываемом методе.
	// Исключение пробрасываем наверх.
	// Изменения переменных сохраняются, даже в случае ошибки!
	
	а = 1;
	Попытка
		ФункцияСОшибкойВПопыткеЗнач(а);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// Перем "а" будет содержать значение 1 (НЕ изменено в вызываемом методе)
	сообщить("Попытка. Исключение было в вызываемом методе. Перем а: "+а);
	
	
	
	
	
	//## Ссылки
	
	
	
	
	
	
	//-------------------------------------------------------------
	// Исключение никуда не пробрасываем, а обрабатываем на месте.
	// Элемент справочника успешно запишется второй раз
	
	спрК1 = справочники.Контрагенты.СоздатьЭлемент();
	спрК1.Наименование = "Контрагент 1";
	спрК1.Записать();
	Попытка
		спрК1.Наименование = "Контрагент 2";
		спрК1.Записать();
		в = 1/0;
	Исключение
		//Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// здесь будет выведено: "Контрагент 2"
	сообщить("Попытка. Справочник.Контрагенты - ОБЪЕКТ. В коде. Исключение никуда не пробрасываем, а обрабатываем на месте. Перем спрК1 = " + спрК1);
	
	
	
	
	
	
	
	//-------------------------------------------------------------
	// Передача ОБЪЕКТА по ссылке - передается ссылка на объект, а не он сам! 
	// Т.е. в данном примере объект будет изменен внутри вызываемого метода!
	// Ошибка в вызываемом методе.
	// Исключение пробрасываем наверх.
	// Элемент справочника успешно запишется второй раз
	
	спрК1 = справочники.Контрагенты.СоздатьЭлемент();
	спрК1.Наименование = "Контрагент 1";
	спрК1.Записать();
	Попытка
		ФункцияСОшибкойВПопыткеСпрКонтрагенты(спрК1);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// здесь будет выведено: "Контрагент 2"
	сообщить("Попытка. Справочник.Контрагенты - ОБЪЕКТ. По ссылке. Исключение было в вызываемом методе. Перем спрК1: "+спрК1);
	
	//-------------------------------------------------------------
	// Передача ОБЪЕКТА по значению - не имеет смысла, будет передано значение ссылки, а не копия объекта. 
	// Т.е. в данном примере объект будет изменен внутри вызываемого метода!
	// Ошибка в вызываемом методе.
	// Исключение пробрасываем наверх.
	// Элемент справочника успешно запишется второй раз
	
	спрК1 = справочники.Контрагенты.СоздатьЭлемент();
	спрК1.Наименование = "Контрагент 1";
	спрК1.Записать();
	Попытка
		ФункцияСОшибкойВПопыткеСпрКонтрагентыЗнач(спрК1);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// здесь будет выведено: "Контрагент 2"
	сообщить("Попытка. Справочник.Контрагенты - ОБЪЕКТ. По значению. Исключение было в вызываемом методе. Перем спрК1: "+спрК1);
	
	
	
	
	
	// Ссылки
	
	//-------------------------------------------------------------
	// Передача ССЫЛКИ по ссылке - передается ссылка на объект, а не он сам! 
	// Т.е. в данном примере объект будет изменен внутри вызываемого метода!
	// Ошибка в вызываемом методе.
	// Исключение пробрасываем наверх.
	// Элемент справочника успешно запишется второй раз
	
	спрК1 = справочники.Контрагенты.СоздатьЭлемент();
	спрК1.Наименование = "Контрагент 1";
	спрК1.Записать();
	Попытка
		ФункцияСОшибкойВПопыткеСпрКонтрагентыСсылка(спрК1.Ссылка);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// здесь будет выведено: "Контрагент 2"
	сообщить("Попытка. Справочник.Контрагенты - ССЫЛКА. По ссылке. Исключение было в вызываемом методе. Перем спрК1: "+спрК1.Ссылка);
	
	//-------------------------------------------------------------
	// Передача ОБЪЕКТА по значению - не имеет смысла, будет передано значение ссылки, а не копия объекта. 
	// Т.е. в данном примере объект будет изменен внутри вызываемого метода!
	// Ошибка в вызываемом методе.
	// Исключение пробрасываем наверх.
	// Элемент справочника успешно запишется второй раз
	
	спрК1 = справочники.Контрагенты.СоздатьЭлемент();
	спрК1.Наименование = "Контрагент 1";
	спрК1.Записать();
	Попытка
		ФункцияСОшибкойВПопыткеСпрКонтрагентыСсылкаЗнач(спрК1.Ссылка);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	// здесь будет выведено: "Контрагент 2"
	сообщить("Попытка. Справочник.Контрагенты - ССЫЛКА. По значению. Исключение было в вызываемом методе. Перем спрК1: "+спрК1.Ссылка);
	
	
	
	
	
	
	
	//------------------------------------------------------------		
	//------------------------------------------------------------	
	//------------------------------------------------------------		
	//------------------------------------------------------------	
	
	
	//# Коллекции. Передача по ссылке и значению
	
	Коллекции_Передача_по_ссылке_и_по_значению();
		
	
	//------------------------------------------------------------		
	//------------------------------------------------------------	
	//------------------------------------------------------------		
	//------------------------------------------------------------	
	
	//# Работа в транзакции
	
	Транзакции();
	
	
	
	

	
	
КонецПроцедуры


Процедура Коллекции_Передача_по_ссылке_и_по_значению()
	
	//## Передача по ссылке - меняет исходный объект
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Счетчик");
	ТЗ.Добавить().Счетчик = 1;
	ТЗ.Добавить().Счетчик = 2;
	сообщить("Коллекции. ТЗ. По ссылке. Исходное количество строк = "+ТЗ.Количество());
	УдалитьИзТЗПередачаПоСсылке(ТЗ);
	//Коллекции. ТЗ. По значению. Новое количество строк = 1
	сообщить("Коллекции. ТЗ. По ссылке. Новое количество строк = "+ТЗ.Количество());
	
	
	//## Передача по значению - тоже меняет исходный объект
	// Почему:
	//При передаче по значению копируется не весь объект, а только его указатель. 
	//Экземпляр объекта остается одним и тем же. Неважно, как вы передаете объект, 
	//по ссылке или по значению - очистка таблицы значений приведет к очистке именно таблицы. 
	//Эта очистка будет видна везде, т.к. объект был один-единственный и неважно, как именно он передавался в метод.	
	//(https://infostart.ru/1c/articles/388527/)
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Счетчик");
	ТЗ.Добавить().Счетчик = 1;
	ТЗ.Добавить().Счетчик = 2;
	сообщить("Коллекции. ТЗ. По значению. Исходное количество строк = "+ТЗ.Количество());
	УдалитьИзТЗПередачаПоЗначению(ТЗ);
	//Коллекции. ТЗ. По значению. Новое количество строк = 1
	сообщить("Коллекции. ТЗ. По значению. Новое количество строк = "+ТЗ.Количество());
	
	//## Можно использовать имя формального параметра для создания коллекции внутри вызываемого метода
	// при передаче по значению - это не повляет на коллекцию внешнего контекста
	
	//в официальной документации
	//https://its.1c.ru/db/metod8dev/content/2606/hdoc
	//есть такой параграф:
	//	Особенности передачи переменных различных типов данных по значению
	//	Передача переменных различных типов данных по значению имеет свои особенности. 
	//	Они заключаются в том, что при работе с методами и свойствами контекста формальных 
	//	параметров может изменяться состояние фактического параметра, передаваемого при вызове процедуры или функции.
	//	Пример 1:
	//	Рассмотрим в качестве примера ситуацию, когда параметром процедуры, передаваемым по значению, 
	//	является таблица значений. Внутри вызываемой процедуры ОбработатьТаблицу() создается новый экземпляр 
	//	таблицы значений, в который добавляется три строки. Это никак не влияет на состояние таблицы значений Тз, 
	//	переданной в качестве фактического параметра при вызове процедуры ОбработатьТаблицу():
	
	// количество строк поменяется - ТЗ переопределяется в вызываемом методе
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Счетчик");
	ТЗ.Добавить().Счетчик = 1;
	ТЗ.Добавить().Счетчик = 2;
	сообщить("Коллекции. ТЗ. По ссылке. Переопределяем исходную коллекцию. Исходное количество строк = "+ТЗ.Количество());
	ИспользоватьФормальныйПараметрПередачаПоСсылке(ТЗ);
	//Коллекции. ТЗ. По значению. Новое количество строк = 3
	сообщить("Коллекции. ТЗ. По ссылке. Переопределяем исходную коллекцию. Новое количество строк = "+ТЗ.Количество());
	
	// количество строк НЕ поменяется 
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Счетчик");
	ТЗ.Добавить().Счетчик = 1;
	ТЗ.Добавить().Счетчик = 2;
	сообщить("Коллекции. ТЗ. По значению. Переопределяем исходную коллекцию. Исходное количество строк = "+ТЗ.Количество());
	ИспользоватьФормальныйПараметрПередачаПоЗначению(ТЗ);
	//Коллекции. ТЗ. По значению. Новое количество строк = 2
	сообщить("Коллекции. ТЗ. По значению. Переопределяем исходную коллекцию. Новое количество строк = "+ТЗ.Количество());
	
	// гораздо понятнее написал Evil Bear   https://infostart.ru/1c/articles/388527/
	//При передаче объекта (например, ТаблицыЗначений) по ссылке, мы передаем само значение указателя (некий handle), который в памяти платформы "держит" объект. При передаче по значению платформа сделает копию этого указателя.
	//Иными словами, если, передавая объект по ссылке, в методе мы присвоим параметру значение "Массив", то в точке вызова получим массив. Повторное присваивание значения, переданного по ссылке, видно из места вызова.

	//Процедура ОбработатьЗначение(Параметр)
	//    Параметр = Новый Массив;
	//КонецПроцедуры
	//Таблица = Новый ТаблицаЗначений;
	//ОбработатьЗначение(Таблица);
	//Сообщить(ТипЗнч(Таблица)); // выведет Массив

	//Если же, мы передадим объект по значению, то в точке вызова наша ТаблицаЗначений не пропадет.	
	
КонецПроцедуры
 

Процедура Транзакции()
		
	//## Примитивы
	
	// эти 2 примера работают одинаково. значение в "а" поменяется на 2
	а = 1;
	НачатьТранзакцию();
	а = 2;
	ЗафиксироватьТранзакцию();
	сообщить(а);
	
	а = 1;
	НачатьТранзакцию();
	а = 2;
	ОтменитьТранзакцию();
	сообщить(а);
	
	
	//## Ссылки
	
	
	спрК = Справочники.Контрагенты.СоздатьЭлемент();
	спрК.Наименование = "спрк1 - 123";
	спрК.Записать();
	
	НачатьТранзакцию();
	
	спрК.Наименование = "спрк1 - 456";
	спрК.Записать();
	
	ЗафиксироватьТранзакцию();
	
	сообщить("объект "+спрК);//456
	сообщить("ссылка "+спрК.ссылка);//456
	
	
	
	спрК2 = Справочники.Контрагенты.СоздатьЭлемент();
	спрК2.Наименование = "спрк2 - 123";
	спрК2.Записать();
	
	НачатьТранзакцию();
	
	спрК2.Наименование = "спрк2 - 456";
	спрК2.Записать();
	
	ОтменитьТранзакцию();
	
	// здесь интереснее.
	// объект в памяти будет хранить в наименовании 456
	// а ссылка - 123, т.к. транзакция отменена
	сообщить("объект "+спрК2);//456
	сообщить("ссылка "+спрК2.Ссылка);//123
	
КонецПроцедуры
 



//------------------------------------------------------------		
//------------------------------------------------------------	
//------------------------------------------------------------		
//------------------------------------------------------------	

// Служебные
	
Функция ФункцияСОшибкойВПопытке(а)
			
	Попытка
		а = 2;
		в = 1/0;
	Исключение
		// здесь перем а будет содержать значение 2
		ВызватьИсключение "Пробрасываем исключение из метода ФункцияСОшибкойВПопытке(). Перем а: "+а;
	КонецПопытки; 
	
КонецФункции
 
Функция ФункцияСОшибкойВПопыткеЗнач(Знач а)
			
	Попытка
		а = 2;
		в = 1/0;
	Исключение
		// здесь перем а будет содержать значение 2
		ВызватьИсключение "Пробрасываем исключение из метода ФункцияСОшибкойВПопытке(). Перем а: "+а;
	КонецПопытки; 
	
КонецФункции

// спрК1 - объект
Функция ФункцияСОшибкойВПопыткеСпрКонтрагенты(спрК1)
		
	Попытка
		спрК1.Наименование = "Контрагент 2";
		спрК1.Записать();
		в = 1/0;
	Исключение
		//Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	Возврат СпрК1.Ссылка;
	
КонецФункции

// спрК1 - объект
Функция ФункцияСОшибкойВПопыткеСпрКонтрагентыЗнач(Знач спрК1)
		
	Попытка
		спрК1.Наименование = "Контрагент 2";
		спрК1.Записать();
		в = 1/0;
	Исключение
		//Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	Возврат СпрК1.Ссылка;
	
КонецФункции
 

// спрК1 - ссылка
Функция ФункцияСОшибкойВПопыткеСпрКонтрагентыСсылка(спрК1)
		
	Попытка
		_спрК1 = спрК1.ПолучитьОбъект();
		_спрК1.Наименование = "Контрагент 2";
		_спрК1.Записать();
		в = 1/0;
	Исключение
		//Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	Возврат _спрК1.Ссылка;
	
КонецФункции

// спрК1 - ссылка
Функция ФункцияСОшибкойВПопыткеСпрКонтрагентыСсылкаЗнач(Знач спрК1)
		
	Попытка
		_спрК1 = спрК1.ПолучитьОбъект();
		_спрК1.Наименование = "Контрагент 2";
		_спрК1.Записать();
		в = 1/0;
	Исключение
		//Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки; 
	
	Возврат _спрК1.Ссылка;
	
КонецФункции


Функция УдалитьИзТЗПередачаПоСсылке(ТЗ)
	
	ТЗ.Удалить(ТЗ.Количество()-1);
	
КонецФункции

Функция УдалитьИзТЗПередачаПоЗначению(Знач ТЗ)
	
	ТЗ.Удалить(ТЗ.Количество()-1);
	
КонецФункции

Функция ИспользоватьФормальныйПараметрПередачаПоСсылке(ТЗ)
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Счетчик");
	ТЗ.Добавить().Счетчик = 1;
	ТЗ.Добавить().Счетчик = 2;
	ТЗ.Добавить().Счетчик = 3;
	
КонецФункции

Функция ИспользоватьФормальныйПараметрПередачаПоЗначению(Знач ТЗ)
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Счетчик");
	ТЗ.Добавить().Счетчик = 1;
	ТЗ.Добавить().Счетчик = 2;
	ТЗ.Добавить().Счетчик = 3;
	
КонецФункции


