В задании есть баг в том, что на сервере будет использоваться каталог клиента.  
Почему.  
НачатьПоискФайлов() - это клиентский метод, соответственно, он может оперировать
только каталогами клиента.  
Хотя, если верить документации, можно использовать сетевой путь или FTP,
тогда ошибки не будет.  
    <Путь> (обязательный)  
    Тип: Строка.   
    Путь к каталогу, в котором производится поиск. Каталог может быть как на локальной сети, так и на ftp-сервере. При использовании этой схемы в адресах необходимо указывать прямые слеши '/', а не обратные '\'.  
    Допускается указание полного имени файла (путь + имя).  

Ниже - рабочий код из обработки импорта АХР (немного упрощен)  

```
// Модуль формы

&НаКлиенте
Процедура StartUpload(Команда)

	СформироватьОтчет = "Протокол загрузки: загрузка не выполнялась";
	КолбэкПомещениеНаСервер = Новый ОписаниеОповещения("КолбэкПомещениеНаСервер", ЭтотОбъект);
	Адрес  = ПоместитьВоВременноеХранилище(Неопределено,Новый УникальныйИдентификатор);
	НачатьПомещениеФайлаНаСервер(КолбэкПомещениеНаСервер,,,Адрес,
        ExcelFile,ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура КолбэкПомещениеНаСервер(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт

	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Адрес       = ОписаниеПомещенногоФайла.Адрес;  //Адрес файла во временном хранилище. 
	FileRef     = ОписаниеПомещенногоФайла.СсылкаНаФайл; //тип СсылкаНаФайл
	Name        = FileRef.Имя;
	StartUploadНаСервере(Адрес, Name);

КонецПроцедуры

&НаСервере
Процедура StartUploadНаСервере(АдресФайлаВремХран, ОригинальноеИмяФайла)
	
	Обработки.sf_ИмпортАХРИзЭксель
    	.Создать()
	    .ВыполнитьИмпортИзЭксельИнтерактивно(
            Неопределено, 
            АдресФайлаВремХран, 
            ОригинальноеИмяФайла);
	
КонецПроцедуры

// Модуль объекта

Процедура ВыполнитьИмпортИзЭксельИнтерактивно(
    Знач АдресаРассылкиПротокола, Знач АдресФайлаВремХран, Знач ОригинальноеИмяФайла) Экспорт
	
    двоичДанные = ПолучитьИзВременногоХранилища(АдресФайлаВремХран);
    времФайл = ПолучитьИмяВременногоФайла("xlsx");
    двоичДанные.Записать(времФайл);

    ЗагрузитьДанныеИзОдногоФайла(времФайл, ОригинальноеИмяФайла);

КонецПроцедуры
```