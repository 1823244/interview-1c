// Как работает код?

В методе Управляемый_дедлок() будет дедлок на записи второго объекта.  
Причина - X-locks держатся до конца транзакции.  
Исправление - в методе Управляемый().  
Попытку не добавлял, чтобы не усложнять код.  
По-умолчанию, таймаут равено 20 секундам, поэтому второй сеанс  
дождется завершения первой транзакции и сможет обновить объекты.  

```
Процедура ПроведениеМногопоточноеНаСервере() Экспорт
	
	ДопПараметры1 = Новый Массив;
	ДопПараметры1.Добавить("Банан");
	ДопПараметры1.Добавить("Апельсин");
	ФоновыеЗадания.Выполнить("sf_concurrency_test.Управляемый",ДопПараметры1, "Поток1");
	
	ДопПараметры2 = Новый Массив;
	ДопПараметры2.Добавить("Апельсин");
	ДопПараметры2.Добавить("Банан");
	ФоновыеЗадания.Выполнить("sf_concurrency_test.Управляемый",ДопПараметры2, "Поток2");
	
		
КонецПроцедуры
```

метод с дедлоком  

```
Процедура Управляемый_дедлок(_1, _2) Экспорт
	
	Контрагент1 = Справочники.Контрагенты.НайтиПоНаименованию(_1);	
	Контрагент2 = Справочники.Контрагенты.НайтиПоНаименованию(_2);
	
	НачатьТранзакцию();
	
	Контрагент = Контрагент1.ПолучитьОбъект();
	Контрагент.Наименование = Контрагент.Наименование + "изм";
	Контрагент.Записать();
	
		
		СУУ_УниверсальныеСервер.Пауза(10);
		
	Контрагент = Контрагент2.ПолучитьОбъект();
	Контрагент.Наименование = Контрагент.Наименование + "изм";
	Контрагент.Записать();
		
	    ЗафиксироватьТранзакцию();
		
	 
		
КонецПроцедуры
```

исправление  

```
Процедура Управляемый(_1, _2) Экспорт
	
	Контрагент1 = Справочники.Контрагенты.НайтиПоНаименованию(_1);	
	Контрагент2 = Справочники.Контрагенты.НайтиПоНаименованию(_2);
	
	Блокировка = Новый БлокировкаДанных;
					
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "Справочник.Контрагенты";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	массив = новый ТаблицаЗначений;
	массив.Колонки.Добавить("Ссылка_");
	массив.Добавить().Ссылка_ = Контрагент1;
	массив.Добавить().Ссылка_ = Контрагент2;
	ЭлементБлокировки.ИсточникДанных = массив;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка","Ссылка_");
	
	
	НачатьТранзакцию();
	
	Блокировка.Заблокировать();
	
	Контрагент = Контрагент1.ПолучитьОбъект();
	Контрагент.Наименование = Контрагент.Наименование + "изм";
	Контрагент.Записать();
	
		
	СУУ_УниверсальныеСервер.Пауза(10);
		
	Контрагент = Контрагент2.ПолучитьОбъект();
	Контрагент.Наименование = Контрагент.Наименование + "изм";
	Контрагент.Записать();
		
	ЗафиксироватьТранзакцию();
		
	 
		
КонецПроцедуры
```